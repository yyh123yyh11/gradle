/*
 * Copyright 2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.util.jar.Attributes

plugins {
    id("gradlebuild.distribution.api-java")
}

gradlebuildJava.usedInWorkers()

dependencies {
    implementation("org.gradle:cli")

    testImplementation("org.gradle:base-services")
    testImplementation("org.gradle:native")
    testImplementation(libs.ant)
    testImplementation(testFixtures("org.gradle:core"))

    integTestImplementation("org.gradle:logging")
    integTestImplementation("org.gradle:core-api")
    integTestImplementation(libs.commonsIo)
    integTestImplementation(libs.littleproxy)
    integTestImplementation(libs.jetty)

    crossVersionTestImplementation("org.gradle:logging")
    crossVersionTestImplementation("org.gradle:persistent-cache")
    crossVersionTestImplementation("org.gradle:launcher")

    integTestNormalizedDistribution("org.gradle:distributions-full")
    crossVersionTestNormalizedDistribution("org.gradle:distributions-full")

    integTestDistributionRuntimeOnly("org.gradle:distributions-full")
    crossVersionTestDistributionRuntimeOnly("org.gradle:distributions-full")
}

val executableJar by tasks.registering(Jar::class) {
    archiveFileName.set("gradle-wrapper.jar")
    manifest {
        attributes.remove(Attributes.Name.IMPLEMENTATION_VERSION.toString())
        attributes(Attributes.Name.IMPLEMENTATION_TITLE.toString() to "Gradle Wrapper")
    }
    from(sourceSets.main.get().output)
    from(configurations.runtimeClasspath.get().incoming.artifactView {
        attributes.attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements.CLASSES))
    }.files)
}

tasks.jar.configure {
    from(executableJar)
}

// === TODO remove and address the following when we have a good reason to change the wrapper jar
executableJar.configure {
    val cliClasspath = layout.buildDirectory.file("gradle-cli-classpath.properties") // This file was accidentally included into the gradle-wrapper.jar
    val cliParameterNames = layout.buildDirectory.file("gradle-cli-parameter-names.properties")  // This file was accidentally included into the gradle-wrapper.jar
    doFirst {
        cliClasspath.get().asFile.writeText("projects=\nruntime=\n")
        cliParameterNames.get().asFile.writeText("")
    }
    from(cliClasspath)
    from(cliParameterNames)
}
strictCompile {
    ignoreRawTypes() // Raw type used in 'org.gradle.wrapper.Install', fix this or add an ignore/suppress annotation there
}
// ===
